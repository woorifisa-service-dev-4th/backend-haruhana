plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.2'
    id 'io.spring.dependency-management' version '1.1.7'

    // SonarQube
    id "org.sonarqube" version "6.0.1.5171"
}

// SonarQube
sonar {
    properties {
        property "sonar.projectKey", "haruhana"
        property "sonar.projectName", "haruhana"
    }
}

group = 'site.haruhana'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    // QueryDSL
    implementation "com.querydsl:querydsl-jpa:5.1.0:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // JWT
    implementation 'io.jsonwebtoken:jjwt:0.12.6'

    // AWS
    // Reference: https://github.com/aws/aws-sdk-java-v2
    implementation platform('software.amazon.awssdk:bom:2.30.16') // BOM 적용. AWS SDK 모듈들이 자동으로 2.30.16 버전에 맞춰짐
    implementation 'software.amazon.awssdk:s3'

    // MySQL
    runtimeOnly 'com.mysql:mysql-connector-j'

    // H2 (Test)
    runtimeOnly 'com.h2database:h2'

    // JUnit 5
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Swagger (API Document)
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.4'
}

tasks.named('test') {
    useJUnitPlatform()
}

// QueryDSL. 컴파일 시점에 기존에 생성된 파일들을 삭제하고 새로 생성
clean {
    delete file('src/main/generated')
}
